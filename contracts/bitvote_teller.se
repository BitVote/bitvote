//  Copyright (C) 07-03-2015 Jasper den Ouden.
//
//  This is free software: you can redistribute it and/or modify
//  it under the terms of the GNU General Public License as published
// by the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.

extern face: [oneperid teller_add_votes:ii]

data face  // (the face, never changes, equal to the changer)
data oneperid // Is allowed to change addresses.

data start_vote_time

// Accounts are split  first bit is frozen/not, next 127, creation time,
//  then 128 "vote time position"
data accounts[]

HALFWAY = 340282366920938463463374607431768211456

def init():
    self.face = msg.sender
    self.start_vote_time = 3600*24*31

def changer_change(pass:arr):
    if msg.sender == self.face:
        self.face = pass[0]
        self.oneperid = pass[1]
        self.start_vote_time = pass[2]

// OnePerID special position priviledges.
def oneperid_add(addr):
    if msg.sender == self.oneperid:
        a = self.accounts[addr]
        if a == 0:  // No address yet
            self.accounts[addr] = 1 + 2*block.timestamp + HALFWAY*(block.timestamp - self.start_vote_time)
        else:
            if a%2 == 0:
                self.accounts[addr] = a + 1  // Unfreeze if it was frozen.

// Freeze accounts.
def oneperid_freeze(addr):
    a = self.accounts[addr]
    if a%2 == 1 & msg.sender == self.oneperid:
        self.accounts[addr] = a - 1

// Remove an account outright.
def oneperid_remove(addr):
    if msg.sender == self.oneperid:
        self.accounts[addr] = 0

 // Change the address that owns it.
def oneperid_move(from, to):
    a = self.accounts[from]
    // oneperid must do it.
    // Address gotta be there,(frozen is allowed)
    // new one may not be taken,
    if a != 0 & msg.sender == self.oneperid & self.accounts[to] == 0:
        self.accounts[to] = a  // Set it.
        self.accounts[from] = 0 // Wipe the old one.

// Voting for those that can.
def vote(on_i, amount):
    a = self.accounts[msg.sender]
    // Exists, not frozen and enough vote time available.
    if a%2 != 0 && a/HALFWAY + amount < block.timestamp:
        self.accounts[msg.sender] = a + HALFWAY*amount
        // NOTE: if the index does not exist, votes are wasted.
        //TODO dammit.. get that to work..
//        self.face.teller_add_votes(on_i, amount, as=face)  // Tell the face immediately.

// Data access.
def changer():
    return self.face
def oneperid():
    return self.oneperid

def account(addr):
    return self.accounts[addr]

def vote_time_pos(addr):
    a = self.accounts[addr]
    return((a%2) * (a/HALFWAY))

def account_created_time(addr):
    return((self.accounts[addr] % HALFWAY) / 2)

def start_vote_time():
    return self.start_vote_time
